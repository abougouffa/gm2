-- Copyright (C) 2001, 2002, 2003, 2004, 2005, 2006, 2007, 2008, 2009, 2010
-- Free Software Foundation, Inc.
-- This file is part of GNU Modula-2.
--
-- GNU Modula-2 is free software; you can redistribute it and/or modify it under
-- the terms of the GNU General Public License as published by the Free
-- Software Foundation; either version 3, or (at your option) any later
-- version.
-- 
-- GNU Modula-2 is distributed in the hope that it will be useful, but WITHOUT ANY
-- WARRANTY; without even the implied warranty of MERCHANTABILITY or
-- FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
-- for more details.
-- 
-- You should have received a copy of the GNU General Public License along
-- with gm2; see the file COPYING.  If not, write to the Free Software
-- Foundation, 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.
--
--
--
-- the ebnf grammer for Modula-2 together with actions for pass 3.
-- pg takes this file and produces a top down parser for Modula-2.
-- The only difference between this grammer and P1 P2 grammer is that
-- we have introduced SilentConstExpr which allows us to parse
-- (with no actions varient record declarations (CASE))
-- and parse with actions constant declarations.
--
% module PCBuild begin
(* Copyright (C) 2001, 2002, 2003, 2004, 2005, 2006, 2007, 2008, 2009, 2010
   Free Software Foundation, Inc.
   This file is part of GNU Modula-2.

GNU Modula-2 is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License as published by the Free
Software Foundation; either version 3, or (at your option) any later
version.
 
GNU Modula-2 is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
for more details.
 
You should have received a copy of the GNU General Public License along
with gm2; see the file COPYING.  If not, write to the Free Software
Foundation, 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA. *)

IMPLEMENTATION MODULE PCBuild ;

FROM M2LexBuf IMPORT currentstring, currenttoken, GetToken, InsertToken, InsertTokenAndRewind, GetTokenNo ;
FROM M2Error IMPORT ErrorStringAt, WriteFormat1, WriteFormat2 ;
FROM NameKey IMPORT NulName, Name, makekey ;
FROM DynamicStrings IMPORT String, InitString, KillString, Mark, ConCat, ConCatChar ;
FROM M2Printf IMPORT printf0 ;
FROM M2Debug IMPORT Assert ;
FROM P2SymBuild IMPORT BuildString, BuildNumber, FixupConstAsString, FixupConstType ;

FROM M2Reserved IMPORT tokToTok, toktype,
                       NulTok, ImportTok, ExportTok, QualifiedTok, UnQualifiedTok,
                       EqualTok, HashTok, LessGreaterTok, LessTok, LessEqualTok,
                       GreaterTok, GreaterEqualTok, InTok, PlusTok, MinusTok,
                       OrTok, TimesTok, DivTok, DivideTok, ModTok, RemTok,
                       AndTok, AmbersandTok, PeriodPeriodTok, ByTok ;

FROM M2Quads IMPORT PushT, PopT, PushTF, PopTF, PopNothing, OperandT, PushTFA,
                    PushT, PushTF, IsAutoPushOn, PushAutoOff, PushAutoOn, PopAuto,
                    BuildTypeForConstructor, BuildConstructor, BuildConstructorEnd,
                    NextConstructorField, SilentBuildConstructor ;

FROM PCSymBuild IMPORT PCStartBuildProgModule,
                       PCEndBuildProgModule,

                       PCStartBuildDefModule,
                       PCEndBuildDefModule,

                       PCStartBuildImpModule,
                       PCEndBuildImpModule,

                       PCStartBuildInnerModule,
                       PCEndBuildInnerModule,

                       PCStartBuildProcedure,
                       PCBuildProcedureHeading,
                       PCEndBuildProcedure,
                       PCBuildImportOuterModule,
                       PCBuildImportInnerModule,
                       StartCurrentConst,
                       EndCurrentConst,
                       SetCurrentConstToString,
                       SetCurrentConstType,
                       SetConstTypeOrExpr ;

FROM SymbolTable IMPORT MakeGnuAsm, PutGnuAsmVolatile, PutGnuAsm, PutGnuAsmInput,
                        PutGnuAsmOutput, PutGnuAsmTrash, PutGnuAsmVolatile,
                        MakeRegInterface,
                        PutRegInterface,
                        GetSymName, GetType, SkipType,
                        NulSym,
                        StartScope, EndScope,
                        PutIncluded,
                        IsVarParam, IsProcedure, IsDefImp, IsModule,
                        IsRecord,
                        RequestSym,
                        GetSym, GetLocalSym,
                        RequestSym ;

FROM M2Batch IMPORT IsModuleKnown ;


CONST
   Debugging = FALSE ;
   Pass1     = FALSE ;

VAR
   WasNoError  : BOOLEAN ;


PROCEDURE ErrorString (s: String) ;
BEGIN
   ErrorStringAt(s, GetTokenNo()) ;
   WasNoError := FALSE
END ErrorString ;


PROCEDURE ErrorArray (a: ARRAY OF CHAR) ;
BEGIN
   ErrorString(InitString(a))
END ErrorArray ;


% declaration PCBuild begin


(*
   SyntaxError - after a syntax error we skip all tokens up until we reach
                 a stop symbol.
*)

PROCEDURE SyntaxError (stopset0: SetOfStop0; stopset1: SetOfStop1; stopset2: SetOfStop2) ;
BEGIN
   DescribeError(stopset0, stopset1, stopset2) ;
   IF Debugging
   THEN
      printf0('\nskipping token *** ')
   END ;
   (*
      yes the ORD(currenttoken) looks ugly, but it is *much* safer than
      using currenttoken<sometok as a change to the ordering of the
      token declarations below would cause this to break. Using ORD() we are
      immune from such changes
   *)
   WHILE NOT (((ORD(currenttoken)<32)  AND (currenttoken IN stopset0)) OR
              ((ORD(currenttoken)>=32) AND (ORD(currenttoken)<64) AND (currenttoken IN stopset1)) OR
              ((ORD(currenttoken)>=64) AND (currenttoken IN stopset2)))
   DO
      GetToken
   END ;
   IF Debugging
   THEN
      printf0(' ***\n')
   END
END SyntaxError ;


(*
   SyntaxCheck - 
*)

PROCEDURE SyntaxCheck (stopset0: SetOfStop0; stopset1: SetOfStop1; stopset2: SetOfStop2) ;
BEGIN
   (* and again (see above re: ORD)
    *)
   IF NOT (((ORD(currenttoken)<32)  AND (currenttoken IN stopset0)) OR
     	   ((ORD(currenttoken)>=32) AND (ORD(currenttoken)<64) AND (currenttoken IN stopset1)) OR
           ((ORD(currenttoken)>=64) AND (currenttoken IN stopset2)))
   THEN
      SyntaxError(stopset0, stopset1, stopset2)
   END
END SyntaxCheck ;


(*
   WarnMissingToken - generates a warning message about a missing token, t.
*)

PROCEDURE WarnMissingToken (t: toktype) ;
VAR
   s0 : SetOfStop0 ;
   s1 : SetOfStop1 ;
   s2 : SetOfStop2 ;
   str: String ;
BEGIN
   s0 := SetOfStop0{} ;
   s1 := SetOfStop1{} ;
   s2 := SetOfStop2{} ;
   IF ORD(t)<32
   THEN
      s0 := SetOfStop0{t}
   ELSIF ORD(t)<64
   THEN
      s1 := SetOfStop1{t}
   ELSE
      s2 := SetOfStop2{t}
   END ;
   str := DescribeStop(s0, s1, s2) ;
   
   str := ConCat(InitString('syntax error,'), Mark(str)) ;
   ErrorStringAt(str, GetTokenNo())
END WarnMissingToken ;


(*
   MissingToken - generates a warning message about a missing token, t.
*)

PROCEDURE MissingToken (t: toktype) ;
BEGIN
   WarnMissingToken(t) ;
   IF (t#identtok) AND (t#integertok) AND (t#realtok) AND (t#stringtok)
   THEN
      IF Debugging
      THEN
         printf0('inserting token\n')
      END ;
      InsertToken(t)
   END
END MissingToken ;


(*
   CheckAndInsert - 
*)

PROCEDURE CheckAndInsert (t: toktype; stopset0: SetOfStop0; stopset1: SetOfStop1; stopset2: SetOfStop2) : BOOLEAN ;
BEGIN
   IF ((ORD(t)<32) AND (t IN stopset0)) OR
      ((ORD(t)>=32) AND (ORD(t)<64) AND (t IN stopset1)) OR
      ((ORD(t)>=64) AND (t IN stopset2))
   THEN
      WarnMissingToken(t) ;
      InsertTokenAndRewind(t) ;
      RETURN( TRUE )
   ELSE
      RETURN( FALSE )
   END
END CheckAndInsert ;


(*
   InStopSet 
*)

PROCEDURE InStopSet (t: toktype; stopset0: SetOfStop0; stopset1: SetOfStop1; stopset2: SetOfStop2) : BOOLEAN ;
BEGIN
   IF ((ORD(t)<32) AND (t IN stopset0)) OR
      ((ORD(t)>=32) AND (ORD(t)<64) AND (t IN stopset1)) OR
      ((ORD(t)>=64) AND (t IN stopset2))
   THEN
      RETURN( TRUE )
   ELSE
      RETURN( FALSE )
   END
END InStopSet ;


(*
   PeepToken - peep token checks to see whether the stopset is satisfied by currenttoken
               If it is not then it will insert a token providing the token
               is one of ; ] ) } . OF END ,

               if the stopset contains <identtok> then we do not insert a token
*)

PROCEDURE PeepToken (stopset0: SetOfStop0; stopset1: SetOfStop1; stopset2: SetOfStop2) ;
BEGIN
   (* and again (see above re: ORD)
    *)
   IF (NOT (((ORD(currenttoken)<32)  AND (currenttoken IN stopset0)) OR
     	    ((ORD(currenttoken)>=32) AND (ORD(currenttoken)<64) AND (currenttoken IN stopset1)) OR
            ((ORD(currenttoken)>=64) AND (currenttoken IN stopset2)))) AND
      (NOT InStopSet(identtok, stopset0, stopset1, stopset2))
   THEN
      (* SyntaxCheck would fail since currentoken is not part of the stopset
         we check to see whether any of currenttoken might be a commonly omitted token *)
      IF CheckAndInsert(semicolontok, stopset0, stopset1, stopset2) OR
         CheckAndInsert(rsbratok, stopset0, stopset1, stopset2) OR
         CheckAndInsert(rparatok, stopset0, stopset1, stopset2) OR
         CheckAndInsert(rcbratok, stopset0, stopset1, stopset2) OR
         CheckAndInsert(periodtok, stopset0, stopset1, stopset2) OR
         CheckAndInsert(oftok, stopset0, stopset1, stopset2) OR
         CheckAndInsert(endtok, stopset0, stopset1, stopset2) OR
         CheckAndInsert(commatok, stopset0, stopset1, stopset2)
      THEN
      END
   END
END PeepToken ;


(*
   Expect - 
*)

PROCEDURE Expect (t: toktype; stopset0: SetOfStop0; stopset1: SetOfStop1; stopset2: SetOfStop2) ;
BEGIN
   IF currenttoken=t
   THEN
      GetToken ;
      IF Pass1
      THEN
         PeepToken(stopset0, stopset1, stopset2)
      END
   ELSE
      MissingToken(t)
   END ;
   SyntaxCheck(stopset0, stopset1, stopset2)
END Expect ;


(*
   CompilationUnit - returns TRUE if the input was correct enough to parse
                     in future passes.
*)

PROCEDURE CompilationUnit () : BOOLEAN ;
BEGIN
   WasNoError := TRUE ;
   FileUnit(SetOfStop0{eoftok}, SetOfStop1{}, SetOfStop2{}) ;
   RETURN( WasNoError )
END CompilationUnit ;


(*
   Ident - error checking varient of Ident
*)

PROCEDURE Ident (stopset0: SetOfStop0; stopset1: SetOfStop1; stopset2: SetOfStop2) ;
BEGIN
   IF IsAutoPushOn()
   THEN
      PushTF(makekey(currentstring), identtok)
   END ;
   Expect(identtok, stopset0, stopset1, stopset2)
END Ident ;


(*
   string -
*)

PROCEDURE string (stopset0: SetOfStop0; stopset1: SetOfStop1; stopset2: SetOfStop2) ;
BEGIN
   IF IsAutoPushOn()
   THEN
      PushTF(makekey(currentstring), stringtok) ;
      BuildString
   END ;
   Expect(stringtok, stopset0, stopset1, stopset2)
END string ;


(*
   Integer -
*)

PROCEDURE Integer (stopset0: SetOfStop0; stopset1: SetOfStop1; stopset2: SetOfStop2) ;
BEGIN
   IF IsAutoPushOn()
   THEN
      PushTF(makekey(currentstring), integertok) ;
      BuildNumber
   END ;
   Expect(integertok, stopset0, stopset1, stopset2)
END Integer ;


(*
   Real -
*)

PROCEDURE Real (stopset0: SetOfStop0; stopset1: SetOfStop1; stopset2: SetOfStop2) ;
BEGIN
   IF IsAutoPushOn()
   THEN
      PushTF(makekey(currentstring), realtok) ;
      BuildNumber
   END ;
   Expect(realtok, stopset0, stopset1, stopset2)
END Real ;

% module PCBuild end
END PCBuild.
% rules
error       'ErrorArray' 'ErrorString'
tokenfunc   'currenttoken'

token   ''                eoftok      -- internal token
token   '+'               plustok
token   '-'               minustok
token   '*'               timestok
token   '/'               dividetok
token   ':='              becomestok
token   '&'               ambersandtok
token   "."               periodtok
token   ","               commatok
token   ";"               semicolontok
token   '('               lparatok
token   ')'               rparatok
token   '['               lsbratok   -- left  square brackets
token   ']'               rsbratok   -- right square brackets
token   '{'               lcbratok   -- left  curly brackets
token   '}'               rcbratok   -- right curly brackets
token   '^'               uparrowtok
token   "'"               singlequotetok
token   '='               equaltok
token   '#'               hashtok
token   '<'               lesstok
token   '>'               greatertok
token   '<>'              lessgreatertok
token   '<='              lessequaltok
token   '>='              greaterequaltok
token   '..'              periodperiodtok
token   ':'               colontok
token   '"'               doublequotestok
token   '|'               bartok
token   'AND'             andtok
token   'ARRAY'           arraytok
token   'BEGIN'           begintok
token   'BY'              bytok
token   'CASE'            casetok
token   'CONST'           consttok
token   'DEFINITION'      definitiontok
token   'DIV'             divtok
token   'DO'              dotok
token   'ELSE'            elsetok
token   'ELSIF'           elsiftok
token   'END'             endtok
token   'EXCEPT'          excepttok
token   'EXIT'            exittok
token   'EXPORT'          exporttok
token   'FINALLY'         finallytok
token   'FOR'             fortok
token   'FROM'            fromtok
token   'IF'              iftok
token   'IMPLEMENTATION'  implementationtok
token   'IMPORT'          importtok
token   'IN'              intok
token   'LOOP'            looptok
token   'MOD'             modtok
token   'MODULE'          moduletok
token   'NOT'             nottok
token   'OF'              oftok
token   'OR'              ortok
token   'PACKEDSET'       packedsettok
token   'POINTER'         pointertok
token   'PROCEDURE'       proceduretok
token   'QUALIFIED'       qualifiedtok
token   'UNQUALIFIED'     unqualifiedtok
token   'RECORD'          recordtok
token   'REM'             remtok
token   'REPEAT'          repeattok
token   'RETRY'           retrytok
token   'RETURN'          returntok
token   'SET'             settok
token   'THEN'            thentok
token   'TO'              totok
token   'TYPE'            typetok
token   'UNTIL'           untiltok
token   'VAR'             vartok
token   'WHILE'           whiletok
token   'WITH'            withtok
token   'ASM'             asmtok
token   'VOLATILE'        volatiletok
token   '...'             periodperiodperiodtok
token   '__DATE__'        datetok
token   '__LINE__'        linetok
token   '__FILE__'        filetok
token   '__ATTRIBUTE__'   attributetok
token   '__BUILTIN__'     builtintok
token   '__INLINE__'      inlinetok
token   'integer number'  integertok
token   'identifier'      identtok
token   'real number'     realtok
token   'string'          stringtok

special Ident             first { < identtok > } follow { }
special Integer           first { < integertok > } follow { }
special Real              first { < realtok > } follow { }
special string            first { < stringtok > } follow { }

BNF

-- the following are provided by the module m2flex and also handbuild procedures below
-- Ident := Letter { ( Letter | Digit ) } =:
-- Integer := Digit { Digit } | OctalDigit { OctalDigit } ( " B " | " C " ) |
--           Digit { HexDigit } " H " =:
-- Real := Digit { Digit } " . " { Digit } [ ScaleFactor ] =:
-- ScaleFactor := " E " [ ( " + " | " - " ) ] Digit { Digit } =:
-- HexDigit := Digit | " A " | " B " | " C " | " D " | " E " | " F " =:
-- Digit := OctalDigit | " 8 " | " 9 " =:
-- OctalDigit := "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" =:
-- String

FileUnit :=                                                                % PushAutoOff %
            ( DefinitionModule |
              ImplementationOrProgramModule )                              % PopAuto %
         =:

ProgramModule := "MODULE"                                                  % PushAutoOn %
                  Ident                                                    % PCStartBuildProgModule %
                                                                           % PushAutoOff %
                  [ Priority
                  ]                                             
                  ";"							   
                  { Import                                                 % PCBuildImportOuterModule %
                  }
                  Block                                                    % PushAutoOn %
                  Ident                                                    % PCEndBuildProgModule %
                  "."                                                      % PopAuto ; PopAuto %
                  =:

ImplementationModule := "IMPLEMENTATION" "MODULE"                          % PushAutoOn %
                         Ident                                             % PCStartBuildImpModule %
                                                                           % PushAutoOff %
                         [ Priority
                         ] ";"                                  
                         { Import                                          % PCBuildImportOuterModule %
                           }                                               
                         Block                                             % PushAutoOn %
                                 					   
                         Ident                                             % PCEndBuildImpModule %
                         "."                                               % PopAuto ; PopAuto ; PopAuto %
                      =:

ImplementationOrProgramModule :=                                           % PushAutoOff %
                                 ( ImplementationModule | ProgramModule )  % PopAuto %
                              =:

Number := Integer | Real =:

Qualident :=                                                               % VAR name: Name;
                                                                                 Type, Sym: CARDINAL ; %
             Ident
                                                                           % IF IsAutoPushOn()
                                                                             THEN
                                                                                PopT(name) ;
                                                                                Sym := RequestSym(name) ;
                                                                                IF IsDefImp(Sym) OR IsModule(Sym)
                                                                                THEN
                                                                                   Expect(periodtok, stopset0, stopset1, stopset2 + SetOfStop2{identtok}) ;
                                                                                   StartScope(Sym) ;
                                                                                   Qualident(stopset0, stopset1, stopset2) ;
                                                                                   (* should we test for lack of ident? *)
                                                                                   PopTF(Sym, Type) ;
                                                                                   PushTF(Sym, Type) ;
                                                                                   PutIncluded(Sym) ;
                                                                                   EndScope
                                                                                ELSE
                                                                                   PushTF(Sym, GetType(Sym))
                                                                                END
                                                                             ELSE (* just parse qualident *) %
             { "." Ident }                                                 % END %
           =:

ConstantDeclaration :=                                                     % PushAutoOn %
                       ( Ident "="                                         % StartCurrentConst %
                                                                           % PushAutoOff %
                                   ConstExpression                         % PopAuto %
                                                    )
                                                                           % EndCurrentConst ;
                                                                             PopAuto %
                     =:

ConstExpression :=                                                         % PushAutoOff %
                   SimpleConstExpr [ Relation SimpleConstExpr
                                   ]                                       % PopAuto %
                =:

Relation := "="   
            | "#" 
            | "<>"
            | "<" 
            | "<="
            | ">" 
            | ">="
            | "IN"
         =:

SimpleConstExpr := UnaryOrConstTerm { AddOperator ConstTerm } =:

UnaryOrConstTerm := "+" ConstTerm | "-" ConstTerm | ConstTerm =:

AddOperator :=   "+" | "-" | "OR" =:

ConstTerm := ConstFactor { MulOperator ConstFactor } =:

MulOperator := "*" | "/" | "DIV" | "MOD" | "REM" | "AND" | "&" =:

ConstFactor := Number | ConstString | ConstSetOrQualidentOrFunction |
               "(" ConstExpression ")" | "NOT" ConstFactor
               | ConstAttribute =:

-- to help satisfy LL1

ConstString := string                                                      % SetCurrentConstToString %
             =:

ComponentElement := ConstExpression [ ".." ConstExpression ] =:

ComponentValue := ComponentElement [ 'BY' ConstExpression ] =:

ArraySetRecordValue := ComponentValue { ','                                % NextConstructorField %
                                            ComponentValue } =:

Constructor := '{'                                                         % SetCurrentConstType ;
                                                                             BuildConstructor %
                  [ ArraySetRecordValue ]                                  % BuildConstructorEnd %
               '}' =:

ConstructorOrConstActualParameters := Constructor |                        % SetCurrentConstType ;
                                                                             PopNothing %
                                                    ConstActualParameters
                                    =:

-- the entry to Constructor

ConstSetOrQualidentOrFunction :=                                           % PushAutoOff %
                                 (
                                   PushQualident
                                   ( ConstructorOrConstActualParameters |  % SetConstTypeOrExpr ;
                                                                             PopNothing %
                                                                          )
                                   |                                       % BuildTypeForConstructor %
                                     Constructor
                                                 )                         % PopAuto %
                               =:

ConstActualParameters := ActualParameters =:

ConstAttribute := "__ATTRIBUTE__" "__BUILTIN__" "(" "("                    % PushAutoOff %
                  ConstAttributeExpression                                 % PopAuto %
                  ")" ")" =:

ConstAttributeExpression :=
                            Ident
                                  | "<" Qualident ',' Ident ">"
                          =:

-- Set := [ Qualident ] "{" [ Element { "," Element } ] "}" =:

Element := ConstExpression [ ".." ConstExpression ] =:

Alignment := [ "__ATTRIBUTE__" "(" "(" Ident "(" ConstExpression ")" ")" ")" ]
           =:

TypeDeclaration := Ident "=" Type Alignment =:							   
									   
Type := 
                                                                           % PushAutoOff %
        ( SimpleType | ArrayType
          | RecordType          
          | SetType             
          | PointerType         
          | ProcedureType )                                                % PopAuto %
      =:								   
									   
SimpleType := Qualident [ SubrangeType ] | Enumeration | SubrangeType =:
									   
Enumeration := "(" IdentList ")" =:

IdentList := Ident                                                         % VAR
                                                                                on: BOOLEAN ;
                                                                                n : CARDINAL ; %
                                                                           % on := IsAutoPushOn() ;
                                                                             IF on
                                                                             THEN
                                                                                n := 1
                                                                             END %
             { "," Ident                                                   % IF on
                                                                             THEN
                                                                                INC(n)
                                                                             END %
             }                                                             % IF on
                                                                             THEN
                                                                                PushT(n)
                                                                             END %
             =:

SubrangeType := "[" ConstExpression  ".." ConstExpression "]" =:

ArrayType := "ARRAY"

              SimpleType
              { "," 
                SimpleType
              } "OF"
             Type
           =:

RecordType := "RECORD"
              FieldListSequence "END" =:


FieldListSequence := FieldListStatement { ";" FieldListStatement } =:

FieldListStatement := [ FieldList ] =:

FieldList := IdentList ":"
                           Type Alignment
             |
             "CASE"
                    CaseTag "OF"
                                 Varient { "|" Varient }
              [ "ELSE"
                FieldListSequence
              ] "END"
           =:

TagIdent := [ Ident ] =:

CaseTag :=  TagIdent [":" Qualident ] =:

Varient := [ VarientCaseLabelList ":" FieldListSequence ] =:

VarientCaseLabelList := VarientCaseLabels { "," VarientCaseLabels } =:

VarientCaseLabels := ConstExpression ( ".." ConstExpression 
                                       |                                   % (* epsilon *) %
                                     )
                   =:

--
--  the following rules are a copy of the ConstExpression ebnf rules but without
--  any actions all prefixed with Silent.
--  At present they are only used by CaseLabels, if this continues to be true we
--  might consider restricting the SilentConstExpression. Eg it makes no sence to allow
--  String in these circumstances!
--

SilentConstExpression :=                                                   % PushAutoOff %
                         SilentSimpleConstExpr
                         [ SilentRelation SilentSimpleConstExpr ]          % PopAuto %
                      =:

SilentRelation := "=" | "#" | "<>" | "<" | "<=" | ">" | ">=" | "IN" =:

SilentSimpleConstExpr := SilentUnaryOrConstTerm { SilentAddOperator SilentConstTerm } =:

SilentUnaryOrConstTerm := "+" SilentConstTerm | "-" SilentConstTerm | SilentConstTerm =:

SilentAddOperator := "+" | "-" | "OR" =:

SilentConstTerm := SilentConstFactor { SilentMulOperator SilentConstFactor } =:

SilentMulOperator := "*" | "/" | "DIV" | "MOD" | "REM" | "AND" | "&" =:

SilentConstFactor := Number | SilentConstString | SilentConstSetOrQualidentOrFunction |
                     "(" SilentConstExpression ")" | "NOT" SilentConstFactor
                     | SilentConstAttribute =:

SilentConstString := string =:

SilentConstAttribute := "__ATTRIBUTE__" "__BUILTIN__" "(" "(" SilentConstAttributeExpression ")" ")" =:

SilentConstAttributeExpression := Ident | "<" Ident ',' SilentConstString ">" =:

SilentComponentElement := SilentConstExpression [ ".." SilentConstExpression ] =:

SilentComponentValue := SilentComponentElement [ 'BY' SilentConstExpression ] =:

SilentArraySetRecordValue := SilentComponentValue { ',' SilentComponentValue } =:

SilentConstructor := '{'                                                   % SilentBuildConstructor %
                         [ SilentArraySetRecordValue ] '}' =:

SilentConstSetOrQualidentOrFunction := SilentConstructor | Qualident
                                       [ SilentConstructor | SilentActualParameters ] =:

SilentElement := SilentConstExpression [ ".." SilentConstExpression ] =:

SilentActualParameters := "(" [ SilentExpList ] ")" =:

SilentExpList := SilentConstExpression { "," SilentConstExpression } =:

-- end of the Silent constant rules

SetType := ( "SET" | "PACKEDSET" ) "OF" SimpleType =:

PointerType := "POINTER" "TO"
                              Type
             =:

ProcedureType := "PROCEDURE"
                 [ FormalTypeList ] =:

FormalTypeList := "(" ( ")" FormalReturn |
                        ProcedureParameters ")" FormalReturn ) =:

FormalReturn := [ ":" OptReturnType ] =:

OptReturnType := "[" Qualident "]" | Qualident =:

ProcedureParameters := ProcedureParameter
                       { "," ProcedureParameter } =:

ProcedureParameter := "..." | "VAR" FormalType | FormalType =:


VarIdent := Ident [ "[" ConstExpression "]" ]
         =:

VarIdentList := VarIdent { "," VarIdent }
              =:

VariableDeclaration := VarIdentList ":" Type Alignment
                    =:

Designator := Qualident { SubDesignator } =:

SubDesignator := "." Ident | "[" ArrayExpList "]" | "^"
            =:

ArrayExpList := Expression { "," Expression } =:

ExpList := Expression { "," Expression } =:

Expression := SimpleExpression [ Relation SimpleExpression ]
           =:

SimpleExpression := UnaryOrTerm { AddOperator Term } =:

UnaryOrTerm := "+" Term | "-" Term | Term =:

Term := Factor { MulOperator Factor } =:

Factor := Number | string | SetOrDesignatorOrFunction |
          "(" Expression ")" | "NOT" ( Factor | ConstAttribute ) =:

PushQualident :=                                                           % VAR name: Name;
                                                                                 Sym : CARDINAL ; %
                                                                           % PushAutoOn %
                        Ident
                                                                           % PopT(name) ;
                                                                             Sym := GetSym(name) ;
                                                                             IF Sym=NulSym
                                                                             THEN
                                                                                PushTFn(NulSym, NulSym, name)
                                                                             ELSIF IsDefImp(Sym) OR IsModule(Sym)
                                                                             THEN
                                                                                Expect(periodtok, stopset0, stopset1, stopset2 + SetOfStop2{identtok}) ;
                                                                                StartScope(Sym) ;
                                                                                PushQualident(stopset0, stopset1, stopset2) ;
                                                                                EndScope
                                                                             ELSE
                                                                                PushTFn(Sym, GetType(Sym), name)
                                                                             END ;
                                                                             PopAuto %
                                                                           % WHILE FALSE DO %
             { "." Ident }                                                 % END %
                    =:


ConstructorOrSimpleDes := Constructor |                                   % PopNothing %
                                          SimpleDes [ ActualParameters ]
                        =:

SetOrDesignatorOrFunction :=                                              % PushAutoOff %
                             (
                             PushQualident
                             ( ConstructorOrSimpleDes |                   % PopNothing %
                                                        )
                                |
                                                                          % BuildTypeForConstructor %
                             Constructor
                             )                                            % PopAuto %
                          =:

-- SimpleDes := { "." Ident | "[" ExpList "]" | "^" } =:
SimpleDes := { SubDesignator } =:

ActualParameters := "(" [ ExpList ] ")" =:

ExitStatement := "EXIT" =:

ReturnStatement := "RETURN" [ Expression ] =:

Statement :=                                                               % PushAutoOff %
             [ AssignmentOrProcedureCall | IfStatement | CaseStatement |
               WhileStatement | RepeatStatement | LoopStatement |
               ForStatement | WithStatement | AsmStatement |
               ExitStatement | ReturnStatement | RetryStatement
             ]                                                             % PopAuto ; %
          =:

RetryStatement := "RETRY" =:

AssignmentOrProcedureCall := Designator ( ":=" Expression |
                                           ActualParameters |              % (* epsilon *) %
                                        ) =:

-- these two break LL1 as both start with a Designator
-- ProcedureCall := Designator [ ActualParameters ] =:
-- Assignment := Designator ":=" Expression =:

StatementSequence :=
                     Statement
                               { ";"
                                     Statement }
                  =:

IfStatement := "IF" Expression "THEN"
                StatementSequence
              { "ELSIF" Expression "THEN" StatementSequence
              }
              [ "ELSE" StatementSequence ] "END"
            =:

CaseStatement := "CASE" Expression "OF" Case { "|" Case }
                 CaseEndStatement
               =:

CaseEndStatement := "END" | "ELSE" StatementSequence "END"
                 =:

Case := [ CaseLabelList ":" StatementSequence ]
      =:

CaseLabelList := CaseLabels { "," CaseLabels } =:

CaseLabels := ConstExpression [ ".." ConstExpression ] =:

WhileStatement := "WHILE" Expression "DO" StatementSequence "END" =:

RepeatStatement := "REPEAT" StatementSequence "UNTIL" Expression =:

ForStatement := "FOR" Ident ":=" Expression "TO" Expression
                [ "BY" ConstExpression ] "DO"
                   StatementSequence
                "END"
              =:

LoopStatement := "LOOP" StatementSequence "END" =:

WithStatement := "WITH" Designator "DO"                                   
                     StatementSequence
                 "END"
               =:

ProcedureDeclaration := ProcedureHeading ";"                               % PushAutoOff %
                                             ProcedureBlock                % PopAuto ; PushAutoOn %
                        Ident                                              % PCEndBuildProcedure ;
                                                                             PopAuto %
                      =:

DefineBuiltinProcedure := [ "__ATTRIBUTE__" "__BUILTIN__"
                            "(" "("                                        % PushAutoOff %
                                    Ident                                  % PopAuto %
                                          ")" ")" | "__INLINE__" ]
                      =:

ProcedureHeading := "PROCEDURE"                                            % PushAutoOn %
                     DefineBuiltinProcedure
                     ( Ident
                                                                           % PCStartBuildProcedure ;
                                                                             PushAutoOff %
                       [ FormalParameters ]
                                                                           % PCBuildProcedureHeading ;
                                                                             PopAuto %
                     )                                                     % PopAuto %
                     =:

Builtin := [ "__BUILTIN__" | "__INLINE__" ] =:

DefProcedureHeading := "PROCEDURE"                                         % PushAutoOn %
                        Builtin
                        ( Ident
                                                                           % PCStartBuildProcedure ;
                                                                             PushAutoOff %
                          [ DefFormalParameters ]
                                                                           % PCBuildProcedureHeading ;
                                                                             PopAuto %
                        )                                                  % PopAuto %
                     =:

-- introduced procedure block so we can produce more informative
-- error messages

ProcedureBlock := { Declaration } [ "BEGIN" ProcedureBlockBody ] "END" =:

Block := { Declaration } InitialBlock FinalBlock "END" =:

InitialBlock := [ "BEGIN" InitialBlockBody ] =:

FinalBlock := [ "FINALLY" FinalBlockBody ] =:

InitialBlockBody := NormalPart [ "EXCEPT" ExceptionalPart ] =:

FinalBlockBody := NormalPart [ "EXCEPT" ExceptionalPart ] =:

ProcedureBlockBody := NormalPart [ "EXCEPT" ExceptionalPart ] =:

NormalPart := StatementSequence =:

ExceptionalPart := StatementSequence =:

Declaration := "CONST" { ConstantDeclaration ";" } |
               "TYPE" { TypeDeclaration ";" } |
               "VAR" { VariableDeclaration ";" } |
               ProcedureDeclaration ";" |
               ModuleDeclaration ";" =:

DefFormalParameters := "(" [ DefMultiFPSection ] ")" FormalReturn =:

DefMultiFPSection := DefExtendedFP |
                     FPSection [ ";" DefMultiFPSection ] =:

FormalParameters := "(" [ MultiFPSection ] ")" FormalReturn =:

MultiFPSection := ExtendedFP |
                  FPSection [ ";" MultiFPSection ] =:

FPSection := NonVarFPSection | VarFPSection =:

DefExtendedFP := DefOptArg | "..." =:

ExtendedFP := OptArg | "..." =:

VarFPSection := "VAR" IdentList ":" FormalType =:

NonVarFPSection := IdentList ":" FormalType =:

OptArg := "[" Ident ":" FormalType [ "=" ConstExpression ] "]" =:

DefOptArg := "[" Ident ":" FormalType "=" ConstExpression "]" =:

FormalType := { "ARRAY" "OF" } Qualident =:

ModuleDeclaration := "MODULE"                                              % PushAutoOn %
                     Ident                                                 % PCStartBuildInnerModule %
                                                                           % PushAutoOff %
                     [ Priority ] ";"
                     { Import                                              % PCBuildImportInnerModule %
                        } [ Export
                            ]
                       Block                                               % PushAutoOn %
                       Ident                                               % PCEndBuildInnerModule %
                                                                           % PopAuto ; PopAuto ; PopAuto %
                     =:

Priority := "[" ConstExpression "]" =:

Export := "EXPORT" ( "QUALIFIED"
                                 IdentList |
                     "UNQUALIFIED"
                                 IdentList |
                     IdentList ) ";" =:

Import :=                                                                  % PushAutoOn %
          ( "FROM" Ident "IMPORT" IdentList ";" |
           "IMPORT"                                                        % PushT(ImportTok)
                                                                             (* determines whether Ident or Module *) %
            IdentList ";" )                                                % PopAuto %
        =:

DefinitionModule := "DEFINITION" "MODULE"                                  % PushAutoOn %
                    [ "FOR" string ]
                    Ident                                                  % PCStartBuildDefModule ;
                                                                             PushAutoOff %
                    ";"
                    { Import                                               % PCBuildImportOuterModule %
                             } [ Export
                                        ]
                    { Definition }
                                   "END"                                   % PushAutoOn %
                                         Ident                             % PCEndBuildDefModule %
                                               "."                         % PopAuto ; PopAuto ; PopAuto %
                  =:

Definition := "CONST" { ConstantDeclaration ";" } |
              "TYPE"
                    { Ident ( ";"                 
                                  | "=" Type Alignment ";" )
                    }
              |
              "VAR" { VariableDeclaration ";" } |
              DefProcedureHeading ";" =:

AsmStatement := 'ASM' [ 'VOLATILE' ] '(' AsmOperands ')' =:

AsmOperands := string [ ':' AsmList [ ':' AsmList [ ':' TrashList ] ] ] =:

AsmList     := [ AsmElement ] { ',' AsmElement } =:

AsmElement := string '(' Expression ')' =:

TrashList := [ string ] { ',' string } =:

FNB
